# Maintainer: Tobias Frilling <tobias@frilling-online.de>
pkgname=guvcview-git
_gitname=guvcview-git-master
pkgver=v1.7.2.16.gfc6c3a1
pkgrel=1
pkgdesc="A video viewer and capturer for the linux uvc driver"
arch=('i686' 'x86_64')
url="http://guvcview.sourceforge.net/"
license=('GPL')
depends=('gtk3' 'ffmpeg' 'portaudio')
makedepends=('git' 'intltool')
optdepends=('pulseaudio: for PulseAudio support')
provides=('guvcview')
conflicts=('guvcview')
source=("$_gitname::git://git.code.sf.net/p/guvcview/git-master")
md5sums=('SKIP')

pkgver() {
  cd "$srcdir/$_gitname"
  git describe --always | sed 's|-|.|g'
}

build() {
  cd "$srcdir/$_gitname"
  ./bootstrap.sh --prefix=/usr
  make
}

package() {
  cd "$srcdir/$_gitname"
  make DESTDIR="$pkgdir/" install
}

# vim:set ts=2 sw=2 et:
